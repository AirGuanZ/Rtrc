CMAKE_MINIMUM_REQUIRED(VERSION 3.20)

PROJECT(RTRC-ALL)

#============= global =============

SET_PROPERTY(GLOBAL PROPERTY USE_FOLDERS ON)

SET(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)
SET(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)
SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)

#============= glfw =============

ADD_SUBDIRECTORY(External/glfw)
SET_TARGET_PROPERTIES(glfw update_mappings PROPERTIES FOLDER "External")

#============= fmt =============

ADD_SUBDIRECTORY(External/fmt)
SET_TARGET_PROPERTIES(fmt PROPERTIES FOLDER "External")

#============= stb =============

ADD_SUBDIRECTORY(External/stb)
SET_TARGET_PROPERTIES(stb PROPERTIES FOLDER "External")

#============= tinyexr =============

ADD_SUBDIRECTORY(External/tinyexr)
SET_TARGET_PROPERTIES(tinyexr PROPERTIES FOLDER "External")

#============= tinyobjloader =============

ADD_SUBDIRECTORY(External/tinyobjloader)
SET_TARGET_PROPERTIES(tinyobjloader PROPERTIES FOLDER "External")

#============= mimalloc =============

ADD_SUBDIRECTORY(External/mimalloc-2.0.6)
SET_TARGET_PROPERTIES(mimalloc-static PROPERTIES FOLDER "External")

#============= backends =============

SET(RTRC_RHI_VULKAN ON)

IF(WIN32)
    SET(RTRC_RHI_DIRECTX12 ON)
ELSE()
    SET(RTRC_RHI_DIRECTX12 OFF)
ENDIF()

#============= vma =============

ADD_LIBRARY(VulkanMemoryAllocator INTERFACE)
TARGET_COMPILE_DEFINITIONS(VulkanMemoryAllocator INTERFACE -DVMA_STATIC_VULKAN_FUNCTIONS=0)
TARGET_COMPILE_DEFINITIONS(VulkanMemoryAllocator INTERFACE -DVMA_DYNAMIC_VULKAN_FUNCTIONS=1)
TARGET_INCLUDE_DIRECTORIES(VulkanMemoryAllocator INTERFACE "External/VulkanMemoryAllocator/include")

#============= dma =============

IF(RTRC_RHI_DIRECTX12)
    ADD_SUBDIRECTORY(External/D3D12MemAlloc)
SET_TARGET_PROPERTIES(D3D12MemAlloc PROPERTIES FOLDER "External")
ENDIF()

#============= volk =============

IF(RTRC_RHI_VULKAN)
    SET(VOLK_HEADERS_ONLY ON)
    ADD_SUBDIRECTORY(External/volk)
ENDIF()

#============= vk-bootstrap =============

IF(RTRC_RHI_VULKAN)
    ADD_SUBDIRECTORY(External/vk-bootstrap)
    SET_TARGET_PROPERTIES(vk-bootstrap PROPERTIES FOLDER "External")
ENDIF()

#============= shader compile =============

ADD_SUBDIRECTORY(External/dxc)
ADD_SUBDIRECTORY(External/SPIRV-Reflect)

#============= rtrc =============

FUNCTION(RTRC_SET_CXX_LANG_VERSION)
    SET(TARGET_NAME ${ARGV0})
    IF(CMAKE_CXX_COMPILER_ID MATCHES "MSVC")
        TARGET_COMPILE_DEFINITIONS(${TARGET_NAME} PUBLIC _CRT_SECURE_NO_WARNINGS)
        SET_PROPERTY(TARGET ${TARGET_NAME} PROPERTY CXX_STANDARD 23)
    ELSEIF(CMAKE_CXX_COMPILER_ID MATCHES "Clang" AND CMAKE_CXX_SIMULATE_ID MATCHES "MSVC")
        TARGET_COMPILE_DEFINITIONS(${TARGET_NAME} PRIVATE _CRT_SECURE_NO_WARNINGS)
        SET_PROPERTY(TARGET ${TARGET_NAME} PROPERTY CXX_STANDARD 23)
    ELSE()
        SET_PROPERTY(TARGET ${TARGET_NAME} PROPERTY CXX_STANDARD 20)
    ENDIF()
    SET_PROPERTY(TARGET ${TARGET_NAME} PROPERTY CXX_STANDARD_REQUIRED ON)
ENDFUNCTION()

FILE(GLOB_RECURSE RTRC_CPP_SRC
		"${CMAKE_CURRENT_SOURCE_DIR}/Source/*.cpp"
		"${CMAKE_CURRENT_SOURCE_DIR}/Source/*.inl"
		"${CMAKE_CURRENT_SOURCE_DIR}/Source/*.h")

FILE(GLOB_RECURSE RTRC_VULKAN_SRC
        "${CMAKE_CURRENT_SOURCE_DIR}/Source/Rtrc/RHI/Vulkan/*.cpp"
        "${CMAKE_CURRENT_SOURCE_DIR}/Source/Rtrc/RHI/Vulkan/*.inl"
        "${CMAKE_CURRENT_SOURCE_DIR}/Source/Rtrc/RHI/Vulkan/*.h")
        
FILE(GLOB_RECURSE RTRC_DIRECTX12_SRC
        "${CMAKE_CURRENT_SOURCE_DIR}/Source/Rtrc/RHI/DirectX12/*.cpp"
        "${CMAKE_CURRENT_SOURCE_DIR}/Source/Rtrc/RHI/DirectX12/*.inl"
        "${CMAKE_CURRENT_SOURCE_DIR}/Source/Rtrc/RHI/DirectX12/*.h")

IF(NOT RTRC_RHI_VULKAN)
    LIST(REMOVE_ITEM RTRC_CPP_SRC ${RTRC_VULKAN_SRC})
ENDIF()

IF(NOT RTRC_RHI_DIRECTX12)
    LIST(REMOVE_ITEM RTRC_CPP_SRC ${RTRC_DIRECTX12_SRC})
ENDIF()

ADD_LIBRARY(Rtrc STATIC ${RTRC_CPP_SRC})

FOREACH(_SRC IN ITEMS ${RTRC_CPP_SRC})
    GET_FILENAME_COMPONENT(SRC "${_SRC}" PATH)
    STRING(REPLACE "${CMAKE_CURRENT_SOURCE_DIR}/Source/Rtrc" "" _GRP_PATH "${SRC}")
    STRING(REPLACE "/" "\\" _GRP_PATH "${_GRP_PATH}")
    SOURCE_GROUP("${_GRP_PATH}" FILES "${_SRC}")
ENDFOREACH()

RTRC_SET_CXX_LANG_VERSION(Rtrc)

TARGET_INCLUDE_DIRECTORIES(Rtrc PUBLIC "${CMAKE_CURRENT_SOURCE_DIR}/Source")
TARGET_LINK_LIBRARIES(Rtrc PUBLIC mimalloc-static fmt)
TARGET_LINK_LIBRARIES(Rtrc PRIVATE stb tinyexr tinyobjloader glfw)

IF(RTRC_RHI_VULKAN OR RTRC_RHI_DIRECTX12)
	TARGET_LINK_LIBRARIES(Rtrc PRIVATE vk-bootstrap volk::volk_headers VulkanMemoryAllocator)
ENDIF()

IF(RTRC_RHI_VULKAN)
	TARGET_LINK_LIBRARIES(Rtrc PRIVATE dxc spirv_reflect vk-bootstrap volk::volk_headers VulkanMemoryAllocator)
ENDIF()

IF(RTRC_RHI_DIRECTX12)
    TARGET_INCLUDE_DIRECTORIES(Rtrc PRIVATE "${CMAKE_CURRENT_SOURCE_DIR}/External/d3dx12")
    TARGET_LINK_LIBRARIES(Rtrc PRIVATE d3d12.lib dxgi.lib d3dcompiler.lib dxguid.lib D3D12MemAlloc)
ENDIF()

#============= rhi flags =============

IF(RTRC_RHI_VULKAN)
    TARGET_COMPILE_DEFINITIONS(Rtrc PUBLIC RTRC_RHI_VULKAN)
ENDIF()

IF(RTRC_RHI_DIRECTX12)
    TARGET_COMPILE_DEFINITIONS(Rtrc PUBLIC RTRC_RHI_DIRECTX12)
ENDIF()

#============= samples =============

SET(RTRC_SAMPLE_WORKING_DIR "${CMAKE_SOURCE_DIR}/")
ADD_SUBDIRECTORY(Samples/01.TexturedQuad)
ADD_SUBDIRECTORY(Samples/02.ComputeShader)
ADD_SUBDIRECTORY(Samples/03.DeferredShading)
