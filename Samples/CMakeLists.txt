cmake_minimum_required(VERSION 3.22)

function(add_rtrc_sample SAMPLE_NAME)
	file(
		GLOB_RECURSE SRCS CONFIGURE_DEPENDS
		"${RTRC_SOURCE_DIR}/Samples/${SAMPLE_NAME}/*.cpp"
		"${RTRC_SOURCE_DIR}/Samples/${SAMPLE_NAME}/*.h")
	file(
		GLOB_RECURSE SHADERS CONFIGURE_DEPENDS
		"${RTRC_SOURCE_DIR}/Samples/${SAMPLE_NAME}/*.shader")
	set(ALL_SRCS ${SRCS} ${SHADERS})
	add_executable(${SAMPLE_NAME} ${ALL_SRCS})
	foreach(SRC IN ITEMS ${ALL_SRCS})
		get_filename_component(SRC_PATH "${SRC}" PATH)
		string(REPLACE "${RTRC_SOURCE_DIR}/Samples/${SAMPLE_NAME}" "${SAMPLE_NAME}" GROUP_PATH "${SRC_PATH}")
		string(REPLACE "/" "\\" GROUP_PATH "${GROUP_PATH}")
		source_group("${GROUP_PATH}" FILES "${SRC}")
	endforeach()
	set_source_files_properties(${SHADERS} PROPERTIES HEADER_FILE_ONLY TRUE)
	set_property(TARGET ${SAMPLE_NAME} PROPERTY CXX_STANDARD 23)
	set_property(TARGET ${SAMPLE_NAME} PROPERTY VS_DEBUGGER_WORKING_DIRECTORY "${RTRC_SOURCE_DIR}")
    set_property(TARGET ${SAMPLE_NAME} PROPERTY FOLDER "Rtrc/Samples")
	target_link_libraries(${SAMPLE_NAME} PUBLIC RtrcToolKit)
	rtrc_register_target(${SAMPLE_NAME})
endfunction()

add_rtrc_sample(01.TexturedQuad)
add_rtrc_sample(02.ComputeShader)
add_rtrc_sample(03.BindlessTexture)
add_rtrc_sample(04.RayTracingTriangle)
add_rtrc_sample(05.PathTracing)
add_rtrc_sample(06.DistortionFreeDisplacementMap)
add_rtrc_sample(07.Gizmo)
add_rtrc_sample(08.FADM)
target_link_libraries(08.FADM PUBLIC libigl)
add_rtrc_sample(09.MeshShader)
add_rtrc_sample(10.FastMarchingMethod)
add_rtrc_sample(11.GeodesicDistance)
add_rtrc_sample(12.ShaderDSL)
add_rtrc_sample(13.HorizonShadowMap)
add_rtrc_sample(14.HalfedgeMesh)
add_rtrc_sample(15.HeatMethod)
add_rtrc_sample(16.MeshCorefinement)
add_rtrc_sample(17.WorkGraph)
add_rtrc_sample(18.ShortestGeodesicPath)
add_rtrc_sample(19.Archive)
add_rtrc_sample(20.MultiLayerOIT)

if(EXISTS "${RTRC_SOURCE_DIR}/Samples/Test.OT2D")
	add_rtrc_sample(Test.OT2D)
endif()
add_rtrc_sample(Test.LIP)
